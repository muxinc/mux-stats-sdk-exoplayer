plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
}

android {
  compileSdk 32

  defaultConfig {
    minSdk 16
    targetSdk 32

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  flavorDimensions 'exoplayer', 'source'
  productFlavors {
    // Source Set with ExoPlayer bindings
    upto2_10 { dimension 'source' }
    from2_10to2_13 { dimension 'source' }
    just2_14 { dimension 'source' }
    just2_15 { dimension 'source' }
    from2_16toNow { dimension 'source' }
    // Exoplayer version for testing/variant matching
    r2_9_6 { dimension 'exoplayer' }
    r2_10_6 { dimension 'exoplayer' }
    r2_11_1 { dimension 'exoplayer' }
    r2_12_1 { dimension 'exoplayer' }
    r2_13_1 { dimension 'exoplayer' }
    r2_14_1 { dimension 'exoplayer' }
    r2_15_1 { dimension 'exoplayer' }
    r2_16_1 { dimension 'exoplayer' }
    r2_17_1 { dimension 'exoplayer' }
    amznPort { dimension 'exoplayer' }
  }
  // We only want the variants where the sourceSet and exoplayer version agree
  def realVariants = [
          "upto2_10": ["r2_9_6", "r2_10_6"],
          "from2_10to2_13": ["r2_11_1", "r2_12_1", "r2_13_1"],
          "just2_14": ["r2_14_1"],
          "just2_15": ["r2_15_1"],
          "from2_16toNow": ["r2_16_1", "r2_17_1"],
  ]
  variantFilter { variant ->
    def exoPlayerFlavor = variant.flavors.findAll { it.dimension == "exoplayer"}[0]
    def sourceFlavor = variant.flavors.findAll { it.dimension == "source"}[0]
    setIgnore(!(realVariants.get(sourceFlavor.name).contains(exoPlayerFlavor.name)))
  }
  sourceSets {
    upto2_10 {
      java.srcDirs += 'src/exo-collector-upto-2_10'
      java.srcDirs += 'src/exo-analytics-upto-2_16/java'
      java.srcDirs += 'src/exo-error-upto-2_13/java'
      java.srcDirs += 'src/exo-event-upto-2_13/java'
      java.srcDirs += 'src/exo-player-upto-2_15/java'
    }
    from2_10to2_13 {
      java.srcDirs += 'src/exo-collector-2_10-now'
      java.srcDirs += 'src/exo-analytics-upto-2_16/java'
      java.srcDirs += 'src/exo-error-upto-2_13/java'
      java.srcDirs += 'src/exo-event-upto-2_13/java'
      java.srcDirs += 'src/exo-player-upto-2_15/java'
    }
    just2_14 {
      java.srcDirs += 'src/exo-collector-2_10-now'
      java.srcDirs += 'src/exo-analytics-upto-2_16/java'
      java.srcDirs += 'src/exo-error-just-2_14/java'
      java.srcDirs += 'src/exo-event-2_14-now/java'
      java.srcDirs += 'src/exo-player-upto-2_15/java'
    }
    just2_15 {
      java.srcDirs += 'src/exo-collector-2_10-now'
      java.srcDirs += 'src/exo-analytics-upto-2_16/java'
      java.srcDirs += 'src/exo-error-2_15-now/java'
      java.srcDirs += 'src/exo-event-2_14-now/java'
      java.srcDirs += 'src/exo-player-upto-2_15/java'
    }
    from2_16toNow {
      java.srcDirs += 'src/exo-collector-2_10-now'
      java.srcDirs += 'src/exo-analytics-2_16-now/java'
      java.srcDirs += 'src/exo-error-2_15-now/java'
      java.srcDirs += 'src/exo-player-2_16-now/java'
      // From 2.16 onward, we don't need exo-event
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

dependencies {

  implementation "com.mux:stats.muxcore:${project.ext.muxCoreVersion}"
  //noinspection GradleDynamicVersion
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.+'
  //noinspection GradleDynamicVersion,GradleDependency
  upto2_10Implementation 'com.google.android.exoplayer:exoplayer:[2.9.6, 2.10.+]'
  //noinspection GradleDynamicVersion,GradleDependency
  from2_10to2_13Implementation 'com.google.android.exoplayer:exoplayer:[2.11.1, 2.13.+]'
  //noinspection GradleDynamicVersion,GradleDependency
  just2_14Implementation 'com.google.android.exoplayer:exoplayer:2.14.+'
  //noinspection GradleDynamicVersion,GradleDependency
  just2_15Implementation 'com.google.android.exoplayer:exoplayer:2.15.+'
  //noinspection GradleDynamicVersion,GradleDependency
  from2_16toNowImplementation 'com.google.android.exoplayer:exoplayer:[2.16.0,2.17.+]'
  //from2_16toNowImplementation 'com.google.android.exoplayer:exoplayer:2.16.+'

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'androidx.test.ext:junit:1.1.3'
  testImplementation "io.mockk:mockk:1.12.3"
  testImplementation 'org.robolectric:robolectric:4.8'
}
