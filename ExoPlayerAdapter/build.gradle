plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
}

android {
  compileSdk 32

  defaultConfig {
    minSdk 16
    targetSdk 32

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  flavorDimensions 'exoplayer'
  productFlavors {
    upto2_13 { dimension 'exoplayer' }
    just2_14 { dimension 'exoplayer' }
    just2_15 { dimension 'exoplayer' }
    from2_16toNow { dimension 'exoplayer' }
  }
  sourceSets {
    upto2_13 {
      java.srcDirs += 'exo-analytics-upto-now/java'
      java.srcDirs += 'exo-error-upto-2_14/java'
      java.srcDirs += 'exo-event-upto-2_13/java'
      java.srcDirs += 'exo-player-upto-2_15/java'
      java.srcDirs += 'exo-common/java'
    }
    just2_14 {
      java.srcDirs += 'exo-analytics-upto-now/java'
      java.srcDirs += 'exo-error-upto-2_14/java'
      java.srcDirs += 'exo-event-2_14_1-now/java'
      java.srcDirs += 'exo-player-upto-2_15/java'
      java.srcDirs += 'exo-common/java'
    }
    just2_15 {
      java.srcDirs += 'exo-analytics-upto-now/java'
      java.srcDirs += 'exo-error-2_15-now/java'
      java.srcDirs += 'exo-event-2_14_1-now/java'
      java.srcDirs += 'exo-player-upto-2_15/java'
      java.srcDirs += 'exo-common/java'
    }
    from2_16toNow {
      java.srcDirs += 'exo-analytics-upto-now/java'
      java.srcDirs += 'exo-error-2_15-now/java'
      java.srcDirs += 'exo-event-2_14-now/java'
      java.srcDirs += 'exo-player-2_16-now/java'
      java.srcDirs += 'exo-common/java'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

dependencies {

  implementation "com.mux:stats.muxcore:${project.ext.muxCoreVersion}"

  //noinspection GradleDynamicVersion,GradleDependency
  upto2_13Implementation 'com.google.android.exoplayer:exoplayer:2.[9.6,13.+]'
  //noinspection GradleDynamicVersion,GradleDependency
  just2_14Implementation 'com.google.android.exoplayer:exoplayer:2.14.+'
  //noinspection GradleDynamicVersion,GradleDependency
  just2_15Implementation 'com.google.android.exoplayer:exoplayer:2.15.+'
  //noinspection GradleDynamicVersion,GradleDependency
  from2_16toNowImplementation 'com.google.android.exoplayer:exoplayer:2.[16,.+'

  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  // TODO: automated tests might not be appropriate for this lib, unit tests should be fine
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}