apply plugin: 'com.android.application'


android {
  compileSdkVersion project.ext.compileSdkVersion

  defaultConfig {
    applicationId "com.mux.stats.sdk.muxstats.automatedtests"
    minSdkVersion 18
    // Target SDK 31 causes manifest merger errors. One of our dependencies is not setting
    //  `android:exported` for an Activity. Error logs have no more useful information
    targetSdkVersion 30 //project.ext.targetSdkVersion
    versionCode 1
    versionName "1.0"
    multiDexEnabled true
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    buildConfigField("boolean", "SHOULD_REPORT_INSTRUMENTATION_TEST_EVENTS_TO_SERVER", "true")
    buildConfigField("String", "INSTRUMENTATION_TEST_ENVIRONMENT_KEY", "\"bar8fjevgudvf4o7r8fdjrfii\"")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    debug {
      minifyEnabled false
    }
  }


  testOptions {
    // Changes the directory where Gradle saves test reports. By default, Gradle saves test reports
    // in the path_to_your_project/module_name/build/outputs/reports/ directory.
    // '$rootDir' sets the path relative to the root directory of the current project.
    reportDir "./automated_test_results/reports"
    // Changes the directory where Gradle saves test results. By default, Gradle saves test results
    // in the path_to_your_project/module_name/build/outputs/test-results/ directory.
    // '$rootDir' sets the path relative to the root directory of the current project.
    resultsDir "./automated_test_results/results"
  }

  sourceSets {
    androidTest {
      // Important, can't get asset file in instrumentation test without this
      assets.srcDirs += 'src/main/assets'
    }
  }

  flavorDimensions 'exoplayer'
  productFlavors {
    r2_14_1 {
      dimension 'exoplayer'
      matchingFallbacks = ['just2_14']
    }
    r2_15_1 {
      dimension 'exoplayer'
      matchingFallbacks = ['just2_15']
    }
    r2_16_1 {
      dimension 'exoplayer'
      matchingFallbacks = ['just2_16']
    }
    r2_17_1 {
      dimension 'exoplayer'
      matchingFallbacks = ['just2_17']
    }
    r2_18_1 {
      dimension 'exoplayer'
      matchingFallbacks = ['just2_18']
    }
    r2_19_1 {
      dimension 'exoplayer'
      matchingFallbacks = ['just2_19']
    }
  }
  variantFilter { // this app doesn't need a release build, so save some resources
    setIgnore(it.buildType.name == 'release')
  }

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }
  lint {
    abortOnError false
    disable 'GradleCompatible'
    lintConfig file('../checker-framework-lint.xml')
  }
  namespace 'com.mux.stats.sdk.muxstats.automatedtests'
}

dependencies {
  implementation fileTree(dir: "libs", include: ["*.jar"])
  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'com.google.android.material:material:1.10.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'androidx.navigation:navigation-fragment:2.7.5'
  implementation 'androidx.navigation:navigation-ui:2.7.5'

  androidTestImplementation 'androidx.test:runner:1.5.2'
  androidTestImplementation 'androidx.test:rules:1.5.0'
  // Optional -- Hamcrest library
  androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
  // Optional -- UI testing with Espresso
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
  // Optional -- UI testing with UI Automator
  androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.5'

  androidTestR2_14_1Implementation 'com.google.ads.interactivemedia.v3:interactivemedia:3.31.0'
  androidTestR2_15_1Implementation 'com.google.ads.interactivemedia.v3:interactivemedia:3.31.0'
  androidTestR2_16_1Implementation 'com.google.ads.interactivemedia.v3:interactivemedia:3.31.0'
  androidTestR2_17_1Implementation 'com.google.ads.interactivemedia.v3:interactivemedia:3.31.0'

//  androidTestR2_18_1Implementation 'com.google.ads.interactivemedia.v3:interactivemedia:3.29.0'
//  androidTestR2_19_1Implementation 'com.google.ads.interactivemedia.v3:interactivemedia:3.29.0'

  api 'org.checkerframework:checker-qual:3.33.0'

  r2_14_1Api 'com.google.android.exoplayer:exoplayer:2.14.1'
  r2_14_1Api 'com.google.android.exoplayer:extension-mediasession:2.14.1'

  r2_15_1Api 'com.google.android.exoplayer:exoplayer:2.15.1'
  r2_15_1Api 'com.google.android.exoplayer:extension-mediasession:2.15.1'

  r2_16_1Api 'com.google.android.exoplayer:exoplayer:2.16.1'
  r2_16_1Api 'com.google.android.exoplayer:extension-mediasession:2.16.1'

  r2_17_1Api 'com.google.android.exoplayer:exoplayer:2.17.1'
  r2_17_1Api 'com.google.android.exoplayer:extension-mediasession:2.17.1'

  r2_18_1Api 'com.google.android.exoplayer:exoplayer:2.18.1'
  r2_18_1Api 'com.google.android.exoplayer:extension-mediasession:2.18.1'
  r2_18_1Api 'com.google.android.exoplayer:extension-ima:2.19.1'

  r2_19_1Api 'com.google.android.exoplayer:exoplayer:2.19.1'
  r2_19_1Api 'com.google.android.exoplayer:extension-mediasession:2.19.1'
  r2_19_1Api 'com.google.android.exoplayer:extension-ima:2.19.1'

  // Automated tests should always test the local module and not the maven dependency.
  implementation project(":MuxExoPlayer")
  implementation project(':ExoPlayerAdapter')
}
